%%%-------------------------------------------------------------------
%%% @author Michal Zmuda
%%% @copyright (C) 2016 ACK CYFRONET AGH
%%% This software is released under the MIT license
%%% cited in 'LICENSE.txt'.
%%% @end
%%%-------------------------------------------------------------------
%%% @doc
%%% This is an OZ mock description used by appmock.
%%% @end
%%%-------------------------------------------------------------------
-module(oz_mock_description).
-author("Michal Zmuda").

-behaviour(mock_app_description_behaviour).

-include_lib("appmock/include/appmock.hrl").
-include_lib("ctool/include/logging.hrl").

-export([rest_mocks/0, tcp_server_mocks/0]).

rest_mocks() ->
    [
        #rest_mock{port = 8443, path = <<"/api/v3/onezone/provider/test/check_my_ip">>,
            response = fun(Req, State) ->
                {{Ip, _Port}, _} = cowboy_req:peer(Req),
                Data = list_to_binary(inet_parse:ntoa(Ip)),
                {#rest_response{code = 200, body = json_utils:encode(Data)}, State}
            end,
            initial_state = #{}},

        #rest_mock{port = 8443, path = <<"/api/v3/onezone/[:endpoint]/[:id]">>,
            response = fun(Req, State) ->
                Endpoint = req:binding(endpoint, Req),
                ID = req:binding(id, Req),
                {Method, _} = cowboy_req:method(Req),
                Body = req:body(Req),
                Params = json_utils:decode(Body),

                case {Endpoint, Method, ID} of
                    {<<"publickey">>, <<"GET">>, <<"special-clear-state-key">>} ->
                        %% this is used to clear the state of mocked OZ
                        %% added only for test purposes
                        {#rest_response{code = 500}, #{}};
                    {<<"publickey">>, <<"GET">>, _} ->
                        ResponseBody = json_utils:encode([
                            {<<"publicKey">>, maps:get(ID, State, undefined)}
                        ]),
                        {#rest_response{code = 200, body = ResponseBody, content_type = <<"application/json">>}, State};
                    {<<"publickey">>, <<"PATCH">>, _} ->
                        EncodedPublicKey = proplists:get_value(<<"publicKey">>, Params),
                        NewState = maps:put(ID, EncodedPublicKey, State),
                        {#rest_response{code = 204}, NewState};
                    {<<"provider_data">>, <<"POST">>, _} ->
                        EncodedPublicKey = proplists:get_value(<<"publicKey">>, Params),
                        NewState = maps:put(ID, EncodedPublicKey, State),
                        {#rest_response{code = 204}, NewState};
                    _Unexpected ->
                        ?error("Unexpected: ~p", [_Unexpected]),
                        {#rest_response{code = 500}, State}
                end
            end,
            initial_state = #{}}
    ].

tcp_server_mocks() -> [].
